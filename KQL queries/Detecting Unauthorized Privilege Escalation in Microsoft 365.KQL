// This KQL query monitors role assignment changes in Azure Active Directory by analyzing audit logs. 
// It filters for specific operations related to adding or removing users from administrative roles. 
// The query identifies who initiated the change (including their IP address), which user was affected, what role was modified, and whether the operation succeeded. 
// This query is essential for detecting potential privilege escalation attempts, tracking administrative changes, and maintaining compliance by creating an audit trail of sensitive permission modifications in the Azure environment.


AuditLogs
| where OperationName in~ ("Add eligible member to role", 
    "Add eligible member to role in PIM completed (permanent)")  // optional : you can add local password update : "Update device local administrator password" 
| extend
    InitiatedByJson = parse_json(InitiatedBy),
    TargetJson = parse_json(TargetResources)
| mv-expand TargetJson
| extend
    Initiator = tostring(InitiatedByJson.user.userPrincipalName),
    InitiatorIP = tostring(InitiatedByJson.user.ipAddress),
    TargetUser = tostring(TargetJson.userPrincipalName),
    RoleName = tostring(TargetJson.modifiedProperties[0])
| project
    TimeGenerated,
    OperationName,
    RoleName,
    TargetJson.displayName,
    Initiator,
    InitiatorIP,
    TargetUser,
    Result,
    ResultDescription,
    TargetResources
| order by TimeGenerated desc
